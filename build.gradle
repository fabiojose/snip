plugins {
    id 'java'
    id 'io.quarkus'
    id 'jacoco'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-picocli'
    implementation 'io.quarkus:quarkus-arc'

    implementation 'org.yaml:snakeyaml:1.27'
    implementation 'org.unix4j:unix4j-command:0.5'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'net.lingala.zip4j:zip4j:2.7.0'

    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.github.everit-org.json-schema:org.everit.json.schema:1.12.2'
    implementation 'org.zeroturnaround:zt-exec:1.12'

    testImplementation 'io.quarkus:quarkus-junit5'

    annotationProcessor enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    annotationProcessor 'info.picocli:picocli-codegen'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
}

group 'com.github.kattlo.snip'
version '0.4.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        csv.enabled true
    }
}

task printClasspath {
   doLast {
       println sourceSets.main.runtimeClasspath.asPath
   }
}
